#! /usr/bin/perl -w
use strict;

#/*@@
#  @file     ConfigurationParser.pl
#  @date     Tue Feb  8 17:36:48 2000
#  @author   Tom Goodale
#  @desc
#            Parser for configuration.ccl files
#  @enddesc
#  @version  $Header$
#@@*/

use lib ".";
my $ccl_file;

#/*@@
#  @routine    CreateConfigurationDatabase
#  @date       Tue Feb  8 17:47:26 2000
#  @author     Tom Goodale
#  @desc
#              Parses the information in all the thorns' configuration.ccl files
#              and creates a database from it
#  @enddesc
#@@*/
sub CreateConfigurationDatabase
{
  my($config_dir, %thorns) = @_;
  my(%cfg) = ();
  my(%thorn_dependencies);

  my $peg_file = $ENV{CCTK_HOME}."/src/piraha/pegs/config.peg";
  my ($grammar,$rule) = piraha::parse_peg_file($peg_file);

  # Loop through each thorn's configuration file.
  foreach my $thorn (sort keys %thorns)
  {
    $ccl_file = "$thorns{$thorn}/configuration.ccl";
    next if (! -r $ccl_file);

    # Get the configuration data from it
    &ParseConfigurationCCL($config_dir, $thorn, \%cfg, \%thorns, $ccl_file, $grammar, $rule);

#    my $debug = 1;
#    if($debug)
#    {
#      print "   $thorn\n";
#      print "           Provides:          ", $cfg{"\U$thorn\E PROVIDES"}, "\n"
#        if ($cfg{"\U$thorn\E PROVIDES"});
#      foreach my $provides (split (' ', $cfg{"\U$thorn\E PROVIDES"}))
#      {
#        print "           as version:        ", $cfg{"\U$thorn\E PROVIDES \U$provides\E VERSION"}, "\n"
#          if ($cfg{"\U$thorn\E PROVIDES \U$provides\E VERSION"});
#      }
#      print "           Requires:          ", $cfg{"\U$thorn\E REQUIRES"}, "\n"
#        if ($cfg{"\U$thorn\E REQUIRES"});
#      print "           Optional:          ", $cfg{"\U$thorn\E OPTIONAL"}, "\n"
#        if ($cfg{"\U$thorn\E OPTIONAL"});
#      print "           Optional-ifactive: ", $cfg{"\U$thorn\E OPTIONAL_IFACTIVE"}, "\n"
#        if ($cfg{"\U$thorn\E OPTIONAL_IFACTIVE"});
#    }

    $cfg{"\U$thorn\E USES THORNS"} = '';

    # verify that all required thorns are there in the ThornList
    next if (! $cfg{"\U$thorn\E REQUIRES THORNS"});

    my @missing = ();
    foreach my $required (split (' ', $cfg{"\U$thorn\E REQUIRES THORNS"}))
    {
      push (@missing, $required)
        if ((! $thorns{"$required"}) && (! $thorns{"\U$required\E"}));
    }
    if (@missing == 1)
    {
      &CST_error (0, "Thorn '$thorn' requires thorn '@missing'. " .
                     'Please add this thorn to your ThornList or remove ' .
                     "'$thorn' from it !");
    }
    elsif (@missing > 1)
    {
      &CST_error (0, "Thorn '$thorn' requires thorns '@missing'. " .
                     'Please add these thorns to your ThornList or ' .
                     "remove '$thorn' from it !");
    }

    $cfg{"\U$thorn\E USES THORNS"} .= $cfg{"\U$thorn\E REQUIRES THORNS"} . ' ';
  }

  if (defined($ENV{VERBOSE}) and lc($ENV{VERBOSE}) eq "yes") {
    print "\n";
    print "+=========================+\n";
    print "| Config Parsing Complete |\n";
    print "+=========================+\n";
  }

  # Turn optional capabilities into required capabilities, if the
  # capability is provided. This way we don't have to treat required
  # and optional requirements differently.
  my %providedcaps;
  foreach my $thorn (sort keys %thorns)
  {
      if ($cfg{"\U$thorn\E PROVIDES"})
      {
          foreach my $providedcap (split (' ', $cfg{"\U$thorn\E PROVIDES"}))
          {
              $providedcaps{$providedcap} = 1;
          }
      }
  }
  foreach my $thorn (sort keys %thorns)
  {
      if ($cfg{"\U$thorn\E REQUIRES"})
      {
          foreach my $requiredcap (split (' ', $cfg{"\U$thorn\E REQUIRES"}))
          {
              $cfg{"\U$thorn\E ACTIVATES"} .= "$requiredcap ";
          }
      }
  }
  foreach my $thorn (sort keys %thorns)
  {
      if ($cfg{"\U$thorn\E OPTIONAL"})
      {
          foreach my $optionalcap (split (' ', $cfg{"\U$thorn\E OPTIONAL"}))
          {
              if ($providedcaps{$optionalcap})
              {
                  $cfg{"\U$thorn\E REQUIRES"} .= "$optionalcap ";
                  $cfg{"\U$thorn\E ACTIVATES"} .= "$optionalcap ";
              }
          }
      }
      if ($cfg{"\U$thorn\E OPTIONAL_IFACTIVE"})
      {
          foreach my $optionalcap (split (' ', $cfg{"\U$thorn\E OPTIONAL_IFACTIVE"}))
          {
              if ($providedcaps{$optionalcap})
              {
                  $cfg{"\U$thorn\E REQUIRES"} .= "$optionalcap ";
                  # nothing is activated
              }
          }
      }
  }

  foreach my $thorn (sort keys %thorns)
  {
    # verify that all required capabilities are there in the ThornList
    next if (! $cfg{"\U$thorn\E REQUIRES"});

    foreach my $requiredcap (split (' ', $cfg{"\U$thorn\E REQUIRES"}))
    {
      my @found = ();
      foreach my $thorncap (sort keys %thorns)
      {
        foreach my $cap (split (' ', $cfg{"\U$thorncap\E PROVIDES"}))
        {
          push (@found, $thorncap)
            if ("\U$cap\E" eq "\U$requiredcap\E");
        }
      }

      # there must be exactly one thorn providing a required capability
      if (@found == 0)
      {
        &CST_error (0, "Thorn '$thorn' requires the capability " .
                       "'$requiredcap'.\n" .
                       "     Please add a thorn that provides '$requiredcap' " .
                       "to your ThornList or remove '$thorn' from it !")
      }
      elsif (@found > 1)
      {
        &CST_error (0, "More than one thorn provides the capability " .
                       "'$requiredcap'.\n" .
                       "     These thorns are: '@found'.\n" .
                       "     Please use only one !\n");
      }
      elsif ( $cfg{"\U$thorn\E REQUIRES \U$requiredcap\E VERSION"} )
      {
        if ( &CheckForCompatibleVersion(
                $cfg{"\U$found[0]\E PROVIDES \U$requiredcap\E VERSION"},
                $cfg{"\U$thorn\E REQUIRES \U$requiredcap\E VERSION"}) == 0 )
        {
          &CST_error (0, "Thorn '$thorn' requires the capability " .
                       "'$requiredcap' in version ".
                       $cfg{"\U$thorn\E REQUIRES \U$requiredcap\E VERSION"}.
                       ". Thorn ".$found[0]." provides $requiredcap, but ".
                       "in version ".
                       $cfg{"\U$found[0]\E PROVIDES \U$requiredcap\E VERSION"}.
                       ".\n");
        }
        $cfg{"\U$thorn\E USES THORNS"} .= $found[0] . ' ';
      }
      else
      {
        $cfg{"\U$thorn\E USES THORNS"} .= $found[0] . ' ';
      }
    }
  }

  # Translate capability to thorn names
  my %capabilities;
  foreach my $thorn (sort keys %thorns)
  {
      next if ! $cfg{"\U$thorn\E PROVIDES"};
      foreach my $cap (split (' ', $cfg{"\U$thorn\E PROVIDES"}))
      {
          $capabilities{"\U$cap\E"} = $thorn;
      }
  }
  foreach my $thorn (sort keys %thorns)
  {
      my $activates = '';
      foreach my $cap (split (' ', $cfg{"\U$thorn\E ACTIVATES"}))
      {
          my $cap_thorn = $capabilities{"\U$cap\E"};
          $activates .= " $cap_thorn";
      }
      $cfg{"\U$thorn\E ACTIVATES THORNS"} = $activates;
  }

  # Check for cyclic dependencies
  # create a hash with thorn-> used thorns (no prefix)
  foreach my $thorn (sort keys %thorns)
  {
    $thorn_dependencies{uc($thorn)}=$cfg{"\U$thorn\E USES THORNS"};
    $thorn_dependencies{uc($thorn)} =~ s/\b$thorn\b//i;
  }

  my $message = &find_dep_cycles(%thorn_dependencies);
  if ("" ne $message)
  {
    $message =~ s/^\s*//g;
    $message =~ s/\s*$//g;
    $message =~ s/\s+/->/g;
    $message = "Found a cyclic dependency in configuration requirements:$message\n";
    &CST_error(0, $message);
  }

  return \%cfg;
}


#/*@@
#  @routine    CompareVersionStrings
#  @date       Tue Oct 20 23:17:18 2015
#  @author     Frank Loeffler
#  @desc
#  Compares two version strings: first non-numeric prefix lexically, next
#  numeric prefix of remainder numerically, and so on.
#  @enddesc
#@@*/
sub CompareVersionStrings
{ 
  my($v1, $v2) = @_;
  my($nan1, $nan2, $num1, $num2, $ret);
  # the loop body strips recognized parts from the strings
  while($v1 ne "" or $v2 ne "") {
    # compare non-numeric prefix followed by numeric value if they exist
    # remove found sub-string from input
    $v1 =~ s/^([^0-9]*)([0-9]*)(.*)/$3/;
    $nan1 = $1;
    $num1 = $2;
    $v2 =~ s/^([^0-9]*)([0-9]*)(.*)/$3/;
    $nan2 = $1;
    $num2 = $2;
    $ret = ($nan1 cmp $nan2) || ($num1 <=> $num2);
    return $ret if ($ret != 0);
  }
  return 0;
}

#/*@@
#  @routine    CheckForCompatibleVersion
#  @date       Tue Oct 20 23:17:18 2015
#  @author     Frank Loeffler
#  @desc
#  Checks that two versions strings are compatible. The first argument is a raw
#  version string, the second argument has also an operator as prefix, which is
#  used to determine if these two match. Returns 1 for success and 0 for failure.
#  @enddesc
#@@*/
sub CheckForCompatibleVersion
{
  my($v1,$fv2) = @_;
  my($op, $v2, $cmp);
  $fv2 =~ m/(<<|<=|=|>=|>>)(.*)/;
  $op = $1;
  $v2 = $2;
  $cmp = &CompareVersionStrings($v1, $v2);
  return 1 if ($op eq '<<' and $cmp <  0);
  return 1 if ($op eq '<=' and $cmp <= 0);
  return 1 if ($op eq '='  and $cmp == 0);
  return 1 if ($op eq '>=' and $cmp >= 0);
  return 1 if ($op eq '>>' and $cmp >  0);
  return 0;
}

#/*@@
#  @routine    ParseConfigurationCCL
#  @date       Tue Feb  8 19:23:18 2000
#  @author     Tom Goodale
#  @desc
#  Parses a configuration.ccl file and generates a database of the values
#  @enddesc
#@@*/
sub ParseConfigurationCCL
{
  my($config_dir, $thorn, $cfg, $thorns, $filename, $grammar, $rule) = @_;
  my(@data);
  my($line_number, $line);
  my($provides, $script, $lang, $options);
  my($optional, $define, $version);
  my(@req_thorns);
  
  $version = "0.0.1";

  # Initialise some stuff to prevent perl -w from complaining.

  my $cfg1 = {};
  $cfg1->{"\U$thorn\E PROVIDES"} = '';
  $cfg1->{"\U$thorn\E REQUIRES"} = '';
  $cfg1->{"\U$thorn\E REQUIRES THORNS"} = '';
  $cfg1->{"\U$thorn\E OPTIONAL"} = '';
  $cfg1->{"\U$thorn\E OPTIONAL_IFACTIVE"} = '';
  $cfg1->{"\U$thorn\E ACTIVATES"} = '';
  $cfg1->{"\U$thorn\E OPTIONS"}  = '';
  my $p=piraha::parse_src($grammar,$rule,$ccl_file);
  my $m = $p->matches();
  unless($m) {
    print "CST ERROR IN FILE '$ccl_file' ";
    $p->showError();
    confess("Parse Error");
  }
    if(defined($ENV{CCTK_MAKE_TREE})) {
      my $fd = new FileHandle;
      open($fd,">tree.txt");
      print $fd $ccl_file,"\n";
      print $fd "=" x 50,"\n";
      print $fd $p->{gr}->dump(),"\n";
      close($fd);
    }

  for my $node (@{$p->{gr}->{children}}) {
    if($node->is("requires")) {
      for my $ch (@{$node->{children}}) {
        if($ch->is("name_with_ver")) {
          my $rname = $ch->group(0,"name")->substring();
          my $key = "\U$thorn\E REQUIRES";
          $cfg1->{$key} .= $rname." ";
          if($ch->has(1,"vop") and $ch->has(2,"vname")) {
            $version = $ch->group(1)->substring() . $ch->group(2)->substring();
            $cfg1->{"\U$thorn REQUIRES $rname VERSION\E"} = $version;
          }
        } elsif($ch->is("name")) {
          my $rname = $ch->substring();
          my $key = "\U$thorn\E REQUIRES";
          $cfg1->{$key} .= $rname." ";
        } elsif($ch->is("thorns")) {
          for my $n (@{$ch->{children}}) {
            my $key = "\U$thorn\E REQUIRES THORNS";
            $cfg1->{$key} .= $n->substring()." ";
            push @req_thorns, $n->substring();
          }
        }
      }
    } elsif($node->is("provopt")) {
      my $key = uc($node->group(0,"key")->substring());
      my $name = $node->group(1,"name")->substring();
      if($key eq "PROVIDES") {
        $cfg1->{"\U$thorn PROVIDES $name OPTIONS"}=[];
        for my $ch (@{$node->{children}}) {
          if($ch->is("name")) {
            my $pname = $ch->substring();
            my $key = "\U$thorn\E PROVIDES";
            $cfg1->{$key} .= $pname." ";
          } elsif($ch->is("version") and $ch->has(0)) {
            $version = $ch->group(0,"vname")->substring();
          } elsif($ch->is("lang") and $ch->has(0)) {
            my $key = "\U$thorn PROVIDES $name LANG";
            $cfg1->{$key} = $ch->group(0,"name")->substring();
          } elsif($ch->is("script") and $ch->has(0)) {
            my $key = "\U$thorn PROVIDES $name SCRIPT";
            $cfg1->{$key} = $thorns->{$thorn}."/".$ch->group(0,"pname")->substring();
          } elsif($ch->is("options")) {
            my $key= "\U$thorn PROVIDES $name OPTIONS";
            my $ropts = [];
            $ropts = $cfg1->{$key} if(defined($cfg1->{$key}));
            my @opts = @{$cfg1->{$key}};
            for my $n (@{$ch->{children}}) {
              push @opts, $n->substring();
            }
            $cfg1->{$key} = \@opts;
          }
        }
        $cfg1->{"\U$thorn PROVIDES $name VERSION\E"} = $version;
      } elsif($key eq "OPTIONAL" or $key eq "OPTIONAL_IFACTIVE") {
        for my $ch (@{$node->{children}}) {
          if($ch->is("name")) {
            my $pname = $ch->substring();
            my $key = "\U$thorn\E $key";
            $cfg1->{$key} .= $pname." ";
          }
        }
      }
    }
  }
  $cfg1->{"\U$thorn\E REQUIRES THORNS"} = join(" ",sort @req_thorns);

  my $cfg2 = {};
  $cfg2->{"\U$thorn\E PROVIDES"} = '';
  $cfg2->{"\U$thorn\E REQUIRES"} = '';
  $cfg2->{"\U$thorn\E REQUIRES THORNS"} = '';
  $cfg2->{"\U$thorn\E OPTIONAL"} = '';
  $cfg2->{"\U$thorn\E OPTIONAL_IFACTIVE"} = '';
  $cfg2->{"\U$thorn\E ACTIVATES"} = '';
  $cfg2->{"\U$thorn\E OPTIONS"}  = '';

  # Read the data
  @data = &read_file($filename);

  for($line_number = 0; $line_number < @data; $line_number++)
  {
    $line = $data[$line_number];
    # Parse the line
    if($line =~ m/^\s*PROVIDES\s*/i)
    {
      $lang = $script = '';
      my ($version);
      ($provides, $script, $lang, $options, $line_number, $version) = &ParseProvidesBlock($line_number, \@data);
      if ($provides !~ m{^[A-Za-z0-9_.]+$}) {
        &CST_error (0, "Illegal capability name '$provides' line '$line' in configure.ccl of thorn '$thorn'");
      }
      if ($lang !~ m{^[A-Za-z0-9_.]*$}) {
        &CST_error (0, "Illegal script language '$lang' line '$line' in configure.ccl of thorn '$thorn'");
      }
      $cfg2->{"\U$thorn\E PROVIDES"} .= "$provides ";
      $cfg2->{"\U$thorn\E PROVIDES \U$provides\E VERSION"} = "$version";
      if($script)
      {
        $cfg2->{"\U$thorn\E PROVIDES \U$provides\E SCRIPT"} = "$thorns->{$thorn}/$script";
      }
      $cfg2->{"\U$thorn\E PROVIDES \U$provides\E LANG"} = $lang;
      $cfg2->{"\U$thorn\E PROVIDES \U$provides\E OPTIONS"} = $options;

#      if ($script)
#      {
#        print "Running configuration script '$script'\n";
#
#        &ParseConfigScript($config_dir, $provides, $lang, $script,
#                           $thorn, $cfg2);
#        print "\n";
#      }

      next;
    }
    elsif($line =~ m/^\s*REQUIRES\s+THORNS\s*:\s*(.*)/i)
    {
      my $newlist = $1;
      $newlist =~ s/\b$thorn\b//i;
      $newlist =~ s/,/ /g;
      my $oldlist = $cfg2->{"\U$thorn\E REQUIRES THORNS"};
      my $list = $oldlist . ' ' . $newlist;
      $list = join (' ', sort split (' ', $list));
      $cfg2->{"\U$thorn\E REQUIRES THORNS"} = $list;
#      if ($cfg2->{"\U$thorn\E REQUIRES THORNS"})
#      {
#        &CST_error (3, '\'Requires Thorns\' will not be supported in release beta-14' .
#        "\n Please adjust thorn \U$thorn\E to use \'Requires\' instead.");
#      }
    }
    elsif($line =~ m/^\s*REQUIRES\s+(.*)/i)
    {
      my $cap = $1;
      if ($cap !~ m{^([A-Za-z0-9_.]+ *(\( *(<<|<=|=|>=|>>) *[0-9a-zA-Z.+-:]+ *\))?)+$}) {
        &CST_error (0, "Illegal required capability '$cap' line '$line' in configure.ccl of thorn '$thorn'");
      }
      while ($cap =~ m/ *([A-Za-z0-9_.]+)( *\((.+)\))?/g)
      {
        my $capability = $1;
        my $version    = $3;
        $version =~ s/ //g;
        $cfg2->{"\U$thorn\E REQUIRES"} .= "$capability ";
        if ($version)
        {
          $cfg2->{"\U$thorn\E REQUIRES \U$capability\E VERSION"} .= "$version";
        }
      }
    }
    elsif($line =~ m/^\s*OPTIONAL\s+/i)
    {
      ($optional, $define, $line_number) = &ParseOptionalBlock($filename, $line_number, \@data);
      if ($optional !~ m{^[A-Za-z0-9_. ]+$}) {
        &CST_error (0, "Illegal optional capability '$optional' line '$line' in configure.ccl of thorn '$thorn'");
      }
      $cfg2->{"\U$thorn\E OPTIONAL"} .= "$optional ";
      $cfg2->{"\U$thorn\E OPTIONAL \U$optional\E DEFINE"} = $define;
    }
    elsif($line =~ m/^\s*OPTIONAL_IFACTIVE\+*/i)
    {
      ($optional, $define, $line_number) = &ParseOptionalBlock($filename, $line_number, \@data);
      if ($optional !~ m{^[A-Za-z0-9_. ]+$}) {
        &CST_error (0, "Illegal optional capability '$optional' line '$line' in configure.ccl of thorn '$thorn'");
      }
      $cfg2->{"\U$thorn\E OPTIONAL_IFACTIVE"} .= "$optional ";
      $cfg2->{"\U$thorn\E OPTIONAL_IFACTIVE \U$optional\E DEFINE"} = $define;
    }
    elsif($line =~ m/^\s*NO_SOURCE\s*$/i)
    {
      $cfg2->{"\U$thorn\E OPTIONS"} .= "NO_SOURCE";
    }
    else
    {
      chomp($line);
      &CST_error (0, "Unrecognised line '$line' in configure.ccl of thorn '$thorn'");
    }
  }

  for my $k (sort keys %$cfg2) {
    my $v1 = $cfg1->{$k};
    my $v2 = $cfg2->{$k};
    my $r1 = ref($v1);
    my $r2 = ref($v2);
    if($r1 ne $r2) {
      confess("ref mismatch for key ($k): ($r1) and ($r2)");
    }
    if($r1 eq "ARRAY" and $r2 eq "ARRAY") {
      $v1 = "\@ARRAY=[".join(",",@$v1)."]";
      $v2 = "\@ARRAY=[".join(",",@$v2)."]";
    }
    $v2 =~ s/\bif\b\s*/if /g;
    $v1 =~ s/ $//;
    $v2 =~ s/ $//;
    $v1 =~ s/\(\s+/\(/g;
    $v2 =~ s/\(\s+/\(/g;
    $v1 =~ s/\s+/\n/g;
    $v2 =~ s/\s+/\n/g;
    $v2 =~ s/\)\{/\)\n\{/g;
    my $fd = new FileHandle;
    open($fd,">v1") or die;
    print $fd $v1,"\n";
    close($fd);
    open($fd,">v2") or die;
    print $fd $v2,"\n";
    close($fd);
    if($v1 ne $v2) {
      my $nk = $k;
      $nk =~ s/\s+[A-Z]+$/ NAME/;
      my $name = $cfg1->{$nk};
      my $name2 = $cfg2->{$nk};
      confess("key error($nk): new:'$name' != old:'$name2'") if($name ne $name2);
      confess("key error($k)($name): new:'$v1' != old:'$v2'");
    }
  }
  for my $k (sort keys %$cfg1) {
    my $v1 = $cfg1->{$k};
    $v1 = "\@ARRAY=[".join(",",@$v1)."]" if(ref($v1) eq "ARRAY");
    if(!defined($cfg2->{$k})) {
      confess("extra key($k)=($v1)");
    }
  }
  for my $k (sort keys %$cfg1) {
    $cfg->{$k} = $cfg1->{$k};
  }
}

#/*@@
#  @routine    ParseProvidesBlock
#  @date       Mon May  8 15:52:40 2000
#  @author     Tom Goodale
#  @desc
#  Parses the PROVIDES block in a configuration.ccl file.
#  @enddesc
#@@*/
sub ParseProvidesBlock
{
  my ($line_number, $data) = @_;
  my ($provides, $script, $lang, $options, $version);

  $provides = "";
  $script   = "";
  $lang     = "";
  $version  = "0.0.1";
  $options  = [];

  $data->[$line_number] =~ m/^\s*PROVIDES\s*(.*)/i;

  $provides = $1;

  $line_number++;
  if($data->[$line_number] !~ m/^\s*\{\s*$/)
  {
    &CST_error (0, "Error parsing provides block line '$data->[$line_number]' $ccl_file:$line_number ".
                   'Missing { at start of block');
    $line_number++ while(defined($data->[$line_number]) and $data->[$line_number] !~ m:\s*\}\s*:);
  }
  else
  {
    while(defined($data->[$line_number]) and $data->[$line_number] !~ m:\s*\}\s*:)
    {
      $line_number++;
      if($data->[$line_number] =~ m/^\s*SCRIPT\s*(.*)$/i)
      {
        $script = $1;
        next;
      }
      elsif($data->[$line_number] =~ m/^\s*LANG[^\s]*\s*(.*)$/i)
      {
        $lang = $1;
        next;
      }
      elsif($data->[$line_number] =~ m/^\s*OPTIONS[^\s]*\s*(.*)$/i)
      {
        push(@$options, split(' ',$1));
        next;
      }
      elsif($data->[$line_number] =~ m/^\s*VERSION\s+(.+)$/i)
      {
        $version = $1;
        if ($1 !~ m/[0-9]([0-9a-z.+-:]*)/i)
        {
          print STDERR "Error in version specification '"+$version+"'. "+
                       "Only alphanumeric characters and . + - : are allowed, "+
                       "and a version has to start with a digit."
          &CST_error (0, 'Unrecognised version');
        }
        next;
      }
      elsif($data->[$line_number] =~ m:\s*\}\s*:)
      {
        # do nothing.
      }
      else
      {
        print STDERR "Error parsing provides block line '$data->[$line_number]'\n";
        &CST_error (0, 'Unrecognised statement');
      }
    }
  }

  return ($provides, $script, $lang, $options, $line_number, $version);
}


#/*@@
#  @routine    ParseOptionalBlock
#  @date       Mon May  8 15:52:40 2000
#  @author     Tom Goodale
#  @desc
#  Parses the OPTIONAL or OPTIONAL_IFACTIVE block in a configuration.ccl file.
#  @enddesc
#@@*/
sub ParseOptionalBlock
{
  my ($file_name, $line_number, $data) = @_;
  my ($optional, $define);

  $data->[$line_number] =~ m/^\s*OPTIONAL(_IFACTIVE)?\s*(.*)/i;

  $optional = $2;

  $define = "";

  $line_number++;

  if($data->[$line_number] !~ m/^\s*\{\s*$/)
  {
    &CST_error (0, "Error parsing optional block line '$data->[$line_number]' $file_name:$line_number".
                ' Missing { at start of block.');
    $line_number++ while(defined($data->[$line_number]) and $data->[$line_number] !~ m:\s*\}\s*:);
  }
  else
  {
    while(defined($data->[$line_number]) and $data->[$line_number] !~ m:\s*\}\s*:)
    {
      $line_number++;
      if($data->[$line_number] =~ m/^\s*DEFINE\s*(.*)$/i)
      {
        if($define eq "")
        {
          $define = $1;
          next;
        }
        else
        {
          &CST_error (0, "Error parsing optional block line '$data->[$line_number]' " . 'Only one define allowed.');
        }
      }
      elsif($data->[$line_number] =~ m:\s*\}\s*:)
      {
        # do nothing.
      }
      else
      {
        &CST_error (0, "Error parsing provides block line '$data->[$line_number]' " . 'Unrecognised statement.');
      }
    }
  }

  return ($optional, $define, $line_number);
}

1;
